plugins {
  id "java"
  id 'jacoco'

  alias libs.plugins.spotless
  alias libs.plugins.versions
  alias libs.plugins.dependencyCheck
  alias libs.plugins.grgit
  alias libs.plugins.semanticVersioning
  alias libs.plugins.nexusPublish
  alias libs.plugins.sonarQube

  id "licensing-defaults"
  id "versioning-defaults"
  id "sonarqube-defaults"
}

subprojects {
  project.version = rootProject.version
  jacoco {
    toolVersion = "0.8.9"
  }
  test {
    finalizedBy jacocoTestReport
  }
  jacocoTestReport {
    dependsOn test
    reports {
      xml.required = false
      csv.required = false
      html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
  }
}

group = gurkenlabs_group

nexusPublishing {
  useStaging.set(!isSnapshotVersion())
  repositories {
    sonatype {
      username = project.findProperty('sonatypeUsername') ?: project.findProperty('OSSRH_USERNAME') ?: ''
      password = project.findProperty('sonatypePassword') ?: project.findProperty('OSSRH_PASSWORD') ?: ''
    }
  }
}


